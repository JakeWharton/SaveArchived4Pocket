apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.sqldelight'
apply plugin: 'app.cash.paparazzi'

archivesBaseName = 'sa4p'

android {
	namespace 'com.jakewharton.sa4p'

	buildFeatures {
		buildConfig = true
		compose = true
	}

	signingConfigs {
		upload {
			storeFile file('upload.keystore')
			storePassword 'sa1234p'
			keyAlias 'sa4p'
			keyPassword 'sa1234p'
		}
	}

	defaultConfig {
		targetSdk 34
		signingConfig signingConfigs.upload

		buildConfigField 'String', 'POCKET_CONSUMER_KEY', "\"${propOrEmpty('SA4P_POCKET_CONSUMER_KEY')}\""

		resConfigs('en')
	}

	buildTypes {
		debug {
			applicationIdSuffix = '.debug'
			versionNameSuffix = 'debug'
		}
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles(file('shrinker-rules.pro'))
		}
	}

	packagingOptions {
		exclude 'okhttp3/internal/publicsuffix/NOTICE'
		exclude 'kotlin/**'
		exclude 'kotlin-tooling-metadata.json'
		exclude 'DebugProbesKt.bin'
		exclude 'META-INF/*.kotlin_module'
		exclude 'META-INF/*.version'
		// TODO This does not actually work https://issuetracker.google.com/issues/295509075.
		exclude 'META-INF/com/android/build/gradle/**'
	}

	lint {
		// // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
		checkReleaseBuilds = false
	}
}

dependencies {
	implementation libs.accompanist.systemUiController
	implementation libs.retrofit.core
	implementation libs.retrofit.kotlinxSerializationConverter
	implementation libs.okhttp.core
	implementation libs.okhttp.logging
	implementation libs.kotlinx.datetime
	implementation libs.kotlinx.serialization.core
	implementation libs.kotlinx.serialization.json
	implementation libs.androidx.compose.material3
	implementation libs.androidx.compose.ui
	implementation libs.androidx.compose.ui.tooling.preview
	implementation libs.androidx.activity.compose
	implementation libs.androidx.work.runtime
	implementation libs.sqldelight.android
	implementation libs.sqldelight.coroutines
	debugImplementation libs.androidx.compose.ui.tooling

	testImplementation libs.junit
	testImplementation libs.testParameterInjector
}

sqldelight {
	databases {
		Database {
			packageName = "com.jakewharton.sa4p.db"
		}
	}
}
